/*
A gradle.properties file is required with the following:
buildDirectory=<directory (e.g. C:\\path\\to\\dir)>
launch4j_icon=<directory e.g. C:\\path\\to\\icon.ico>
*/

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'org.mini2Dx.butler'

def version = '0.2.1'
def buildDir = project.hasProperty('buildDirectory') ? buildDirectory : System.getenv('TRAVIS_BUILD_DIR')

shadowJar {
    baseName = 'LostSector'
    this.version = null
    classifier = null
    destinationDir = new File(buildDir.toString())
}

launch4j {
    mainClassName = 'com.ezardlabs.lostsector.Main'
    icon = project.hasProperty('launch4j_icon') ? launch4j_icon : System.getenv('launch4j_icon')
    outputDir = project.hasProperty('buildDirectory') ? buildDirectory : System.getenv ('TRAVIS_BUILD_DIR')
    jar = (project.hasProperty('buildDirectory') ? (project.projectDir.toString() + buildDirectory) : System.getenv('TRAVIS_BUILD_DIR')) + 'LostSector.jar'
    outfile = 'LostSector.exe'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:2.3.0'
        classpath group: 'org.mini2Dx', name: 'butler', version: '1.1.2'
    }
}

dependencies {
    compile project(':lost-sector')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

project(':lost-sector') {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = "com.github.8-Bit-Warframe"

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':dethsquare')
    }
}

butler {
    user = '8bitwarframe'
    game = 'lost-sector'
    println "Version: " + version
    userVersion = version

    anyOs {
        binDirectory = file('build/install/LostSector').getAbsolutePath()
        channel = 'win-osx-linux-alpha'
    }
}

task clearInstallDirectory {
    doFirst {
        file('build/install/LostSector').deleteDir()
    }
}

task moveLaunchScripts {
    doLast {
        File windowsA = file('build/install/LostSector/bin/LostSector.bat')
        File windowsB = file('build/install/LostSector/LostSector-Windows.bat')

        File unixA = file('build/install/LostSector/bin/LostSector')
        File unixB = file('build/install/LostSector/LostSector-OSX-Linux')

        windowsB.createNewFile()
        windowsB.withWriter { writer ->
            windowsA.eachLine { line ->
                if (line == 'set APP_HOME=%DIRNAME%..') {
                    writer << 'set APP_HOME=%DIRNAME%'
                } else {
                    writer << line
                }
                writer << System.lineSeparator()
            }
        }

        unixB.createNewFile()
        unixB.withWriter { writer ->
            unixA.eachLine { line ->
                if (line == 'cd "`dirname \\"$PRG\\"`/.." >/dev/null') {
                    writer << 'cd "`dirname \\"$PRG\\"`/" >/dev/null'.replace('\r', '')
                } else {
                    writer << line.replace('\r', '')
                }
                writer << System.lineSeparator()
            }
        }

        file('build/install/LostSector/bin').deleteDir()
    }
}
installDist.dependsOn clearInstallDirectory
moveLaunchScripts.dependsOn installDist
butlerPush.dependsOn moveLaunchScripts

mainClassName = 'com.ezardlabs.lostsector.Main'